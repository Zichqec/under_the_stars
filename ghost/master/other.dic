OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	//recoding this again from scratch, no peeking at old code...
	if !("\![no-autopause]" _in_ _talk)
	{
		_talk = REPLACE(_talk,"... ",".\w4.\w4.\w8\w8 ")
		_talk = REPLACE(_talk,"...? ",".\w4.\w4.\w4?\w8\w8 ")
		_talk = REPLACE(_talk,"...! ",".\w4.\w4.\w4!\w8\w8 ")
		_talk = REPLACE(_talk,". ",".\w8\w8 ")
		_talk = REPLACE(_talk,", ",",\w8 ")
		_talk = REPLACE(_talk,"! ","!\w8\w8 ")
		_talk = REPLACE(_talk,"? ","?\w8\w8 ")
	}
	
	_talk = REPLACE(_talk,"\b[6]","\b[6,--fallback=0]")
	
	TOSTR(_talk)
}

OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	"\1\s[-1]"
	--
	"\0\s[0]"
}

OnWindowStateRestore
{
	OnSurfaceRestore
}

On_homeurl
{
	"https://raw.githubusercontent.com/Zichqec/under_the_stars/main/"
}

br
{
	//Yes I am coding this from scratch
	if SHIORI3FW.BalloonName == "Reflections of Starlight"; "\n"
	else; " "
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}


On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Galla";					"https://gallathegalla.github.io/gtg-ghosts/"
	"YudiDoodles";				"https://www.tumblr.com/yudidoodles"
	"Zichqec";					"https://ukagaka.zichqec.com/"
	"おもちよいち (OmochiYoichi)";	"https://sites.google.com/view/omochi-yoichi/"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Wiki page";	"https://ukagakadreamteam.com/wiki/ghost/under_the_stars"
}
