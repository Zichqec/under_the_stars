OnMouseDoubleClick
{
	if reference5 == 0 && reference0 == 0; OnSakuraMenu
}

OnSakuraMenu : all
{
	"\C\![lock,balloonrepaint]\c\0\s[0]\b[0]\![set,autoscroll,disable]\_q"
	
	if SHIORI3FW.BalloonName == "Reflections of Starlight"; "\n[300]"
	
	"\![*]\q[Reflect,OnAiTalk]"
	if LastTalk != ""; "  \![*]\q[Revisit reflection,OnLastTalk]"
	"\n\n"
	
	"\![*]\q[Wonderings,OnTalkRateMenu]\n\n"
	
	"\![*]Frequency of reflections:\n     "
	_talkrate = ("30,30s","60,1m","180,3m","300,5m","600,10m")
	foreach _talkrate; _rate
	{
		switch aitalkinterval == TOINT(_rate[0])
		{
			"\__q[OnTalkRateChange,%(_rate[0])]%(_rate[1])\__q"
			"\f[underline,1]\_a[OnTalkRateChange,%(_rate[0])]%(_rate[1])\_a\f[underline,default]"
		}
		"  "
	}
	
	"\n\n"
	
	"\![*]\q[Cancel,blank]"
	"\![unlock,balloonrepaint]"
}

OnLastTalk
{
	LastTalk
}

OnTalkRateMenu : all
{	
	//This is just setting up a label, you may need to add to it or tweak it if you change what rates are available
	_talkrate = "Off"
	if aitalkinterval == 30; _talkrate = "30 seconds"
	elseif aitalkinterval == 60; _talkrate = "1 minute"
	elseif aitalkinterval == 180; _talkrate = "3 minutes"
	elseif aitalkinterval == 300; _talkrate = "5 minutes"
	elseif aitalkinterval == 600; _talkrate = "10 minutes"
	
	"\0\s[0]\b[0]\![set,autoscroll,disable]\_q"
	"Current interval: %(_talkrate)\n\n"
	
	"\![*]\q[Off,OnTalkRateChange,0]\n"
	"\![*]\q[30 seconds,OnTalkRateChange,30]\n"
	"\![*]\q[1 minute,OnTalkRateChange,60]\n"
	"\![*]\q[3 minutes,OnTalkRateChange,180]\n"
	"\![*]\q[5 minutes,OnTalkRateChange,300]\n"
	"\![*]\q[10 minutes,OnTalkRateChange,600]\n"
	"\n"
	"\![*]\q[Back,OnSakuraMenu]  \![*]\q[Nevermind,blank]"
}

OnTalkRateChange
{
	aitalkinterval = TOINT(reference0)
	OnSakuraMenu
}